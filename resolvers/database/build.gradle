description = "Externalized Properties Database Resolver"

dependencies {
  implementation project(":core")
  testImplementation(testFixtures(project(":core")))
  testImplementation "com.h2database:h2:2.1.214"
}

jar {
  manifest {
    attributes(
      "Automatic-Module-Name": "io.github.joeljeremy7.externalizedproperties.resolvers.database"
    )
  }
}

testing {
  suites {
    integrationTest(JvmTestSuite) {
      dependencies {
        implementation project
        // Fix when https://github.com/gradle/gradle/issues/19192 is resolved.
        implementation(project.dependencies.testFixtures(project))
        implementation project(":core")
        // Fix when https://github.com/gradle/gradle/issues/19065 is resolved.
        implementation(project.dependencies.platform("org.testcontainers:testcontainers-bom:1.17.4"))
        implementation "org.testcontainers:junit-jupiter"
        implementation "org.testcontainers:postgresql"
        implementation "org.testcontainers:mysql"
        implementation "org.testcontainers:oracle-xe"
        implementation "org.testcontainers:mssqlserver"
        implementation "com.zaxxer:HikariCP:5.0.1"
        runtimeOnly "org.postgresql:postgresql:42.5.0"
        runtimeOnly "mysql:mysql-connector-java:8.0.30"
        runtimeOnly "com.oracle.database.jdbc:ojdbc8:21.7.0.0"
        runtimeOnly "com.microsoft.sqlserver:mssql-jdbc:11.2.1.jre8"
      }
    }
  }
}